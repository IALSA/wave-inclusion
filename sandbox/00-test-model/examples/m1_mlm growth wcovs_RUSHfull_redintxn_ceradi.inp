  TITLE:  ! Your notes that appear in output file
      Rush MAP (CERAD) linear growth model for IALSA
      tri-study coordinated analysis;

  DATA: ! FILE is location of data file
      FILE = Data/MAP_ADK_141029.dat;
      FORMAT = FREE;           ! Free is default
      TYPE = INDIVIDUAL;       ! Individual data is default

  VARIABLE: NAMES ARE
       PID blage died yrseduc sex race 
       spanish blHTN blDM blCHF blMI blSTK  
       eversmoke fu_year agevisit time agedeath
       logmem1a logmem2a ceradi ceradd ceradr 
       dord ebdr ebir mmse dcfdx dementia cogflag;

  ! Variables to be analyzed in this model
      USEVARIABLES ARE PID sex eversmoke blCHF blMI blSTK
      blHTN blDM time age ed age_sex blHTNDM
      age_htn age_dm sex_htn sex_dm y;

  ! Missing data identifier
      MISSING ARE ALL (999);

   USEOBSERVATIONS ARE cogflag EQ 0;

  ! Person-level ID variable
      CLUSTER = PID;

  ! Within-person predictors
      WITHIN = time;

  ! Between-person predictors
      BETWEEN = age sex ed age_sex eversmoke blCHF blMI blSTK
      blHTN blDM blHTNDM
      age_htn age_dm sex_htn sex_dm;

  DEFINE:  ! Creates new variables
      age = blage-80;
      ed  = yrseduc-11;
      age_sex = age*sex;
      blHTNDM = blHTN*blDM;
      age_htn = age*blHTN;
      age_dm = age*blDM;
  !    agehtndm = age*htn_dmw1;
      sex_htn = sex*blHTN;
      sex_dm = sex*blDM;
  !    sexhtndm = female*htn_dmw1;
      y = ceradi;
  !    age_sex = age*sex;
  !    age_ed  = age*ed;
  !    sex_ed  = sex*ed;

  ANALYSIS: ! Estimation options
      TYPE = TWOLEVEL RANDOM; ESTIMATOR=MLR;
      !TYPE IS TWOLEVEL RANDOM;        ! not sure if this works with stratification
      !ESTIMATOR IS ML;                ! but is what was used in ELSA MLM approach

  MODEL:

  ! Level-1, time-level model
      %WITHIN%                                     ! Residual included by default
           linear | y ON time;                  ! Fixed linear time slope

  ! Level-2, person-level model
      %BETWEEN%
           y*;                                  ! Random intercept included by default
           linear*;                                ! Random linear time slope
           y WITH linear*;                      ! Intercept-linear covariance
           y linear ON sex age ed eversmoke blCHF blMI blSTK
           blHTN blDM blHTNDM age_sex age_htn age_dm 
           sex_htn sex_dm ;
  ! Same thing as doing each one separately as below?
  !         MMSE linear ON age*;                    ! Baseline age on intercept and slope
  !         MMSE linear ON sex*;                    ! Sex on intercept and slope
  !         MMSE linear ON ed*;                     ! Education on intercept and slope
  !         MMSE linear ON age_sex*;                ! Age*Sex on intercept and slope
  !         MMSE linear ON age_ed*;                 ! Age*Educ on intercept and slope
  !         MMSE linear ON sex_ed*;                 ! Sex*Educ on intercept and slope

  OUTPUT:                      ! Request additional output as needed
      SAMPSTAT; STDYX;              ! Sample descriptives to check data accuracy
PLOT:
 TYPE IS plot1 plot2 plot3;